extends Resource

"""
A material whose maps are generated by blending several `MaterialLayers`

`layers` contains the `MaterialLayers`, each of which
can have multiple channels enabled.
When generating the results, all `LayerTexture`s of each map
are blended together and stored in the `results` `Dictionary`.
It stores the blended `Texture`s with the map names as keys.

To make it possible to use Viewports inside of sub-resources of MaterialLayers,
this and every `Resource` class that is used inside of it has to be local to scene.
"""

export var layers : Array

var results : Dictionary
var busy := false

signal results_changed

const TextureLayer = preload("res://resources/texture/texture_layer.gd")
const BlendingLayer = preload("res://addons/layer_blending_viewport/layer_blending_viewport.gd").BlendingLayer
const LayerTexture = preload("res://resources/texture/layer_texture.gd")
const MaterialFolder = preload("res://resources/material/material_folder.gd")

func _init() -> void:
	resource_local_to_scene = true
	# for some reason, NOTIFICATION_POSTINITIALIZE doesn't fire,
	# so use this hack instead
	yield(VisualServer, "frame_post_draw")
	for layer in layers:
		layer.parent = self


func add_layer(layer, onto) -> void:
	layer.parent = onto
	onto.layers.append(layer)
	update()


func delete_layer(layer) -> void:
	layer.parent.layers.erase(layer)
	update()


func update() -> void:
	if busy:
		return
	busy = true
	var flat_layers := _get_flat_layers(layers, false)
	
	for layer in flat_layers:
		var result = layer.update()
		if result is GDScriptFunctionState:
			result = yield(result, "completed")
	
	for map in Globals.TEXTURE_MAP_TYPES:
		var blending_layers := []
		for layer in flat_layers:
			if not (map in layer.maps and layer.maps[map]):
				continue
			var map_layer_texture : LayerTexture = layer.maps[map]
			map_layer_texture.update(true)
			
			var blending_layer : BlendingLayer
			if layer.mask:
				layer.mask.update(true)
				blending_layer = BlendingLayer.new(
					"texture({layer_result}, uv)", "normal", 1.0, layer.mask.result)
			else:
				blending_layer = BlendingLayer.new("texture({layer_result}, uv)")
			blending_layer.uniform_types.append("sampler2D")
			blending_layer.uniform_names.append("layer_result")
			blending_layer.uniform_values.append(map_layer_texture.result)
			blending_layers.append(blending_layer)
		
		if blending_layers.empty():
			results.erase(map)
			continue
		
		var result : Texture = yield(LayerBlendViewportManager.blend(
				blending_layers, Globals.result_size,
				get_instance_id() + map.hash()), "completed")
		
		if map == "height":
			map = "normal"
			result = yield(NormalMapGenerationViewport.get_normal_map(result),
					"completed")
		results[map] = result
	busy = false
	emit_signal("results_changed")


func get_material(existing : SpatialMaterial = null) -> SpatialMaterial:
	var material_maps = Globals.TEXTURE_MAP_TYPES.duplicate()
	material_maps.erase("height")
	material_maps.append("normal")
	
	var material : SpatialMaterial = existing
	if not existing:
		material = preload("res://3d_viewport/material.tres").duplicate()
	
	for map in material_maps:
		if map in results.keys():
			material.set(map + "_enabled", true)
			material.set(map + "_texture", results[map])
		else:
			material.set(map + "_enabled", false)
			material.set(map + "_texture", null)
		
		if map == "metallic":
			material.set("metallic", int(map in results.keys()))
	
	return material


func _get_flat_layers(layer_array : Array = layers, add_hidden := true) -> Array:
	var flat_layers := []
	for layer in layer_array:
		if (not add_hidden) and not layer.visible:
			continue
		if layer is MaterialFolder:
			flat_layers += _get_flat_layers(layer.layers, add_hidden)
		else:
			flat_layers.append(layer)
	return flat_layers
